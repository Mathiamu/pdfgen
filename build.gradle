buildscript {
    ext.kotlin_version = '1.2.51'
    ext.ktor_version = '0.9.3'
    ext.prometheus_version = '0.3.0'
    ext.logback_version = '1.2.3'
    ext.logstash_encoder_version = '5.1'
    ext.open_html_to_pdf_version = '0.0.1-RC15'
    ext.handlebars_version = '4.1.0'
    ext.handlebars_jackson_version = '1.0.0'
    ext.jsoup_version = '1.11.3'
    ext.jackson_version = '2.9.5'
    ext.junit_version = '4.12'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'org.jmailen.kotlinter' version '1.14.0'
    id 'org.jetbrains.kotlin.jvm' version "1.2.51"
    id 'application'
}

group 'no.nav.pdfgen'
version '1.0.5-SNAPSHOT'

mainClassName = 'no.nav.pdfgen.BootstrapKt'

applicationDefaultJvmArgs = [
        '-XX:+UnlockExperimentalVMOptions',
        '-XX:+UseCGroupMemoryLimitForHeap',
        '-XX:+UseG1GC',
        '-XshowSettings:vm',
        '-Dlogback.configurationFile=logback-remote.xml'
]

// suppress warning for experimental coroutines
kotlin { experimental { coroutines "enable"} }

task printVersion() {
    doLast {
        println(version)
    }
}

test {
    useJUnitPlatform {
        includeEngines 'spek2'
    }
    testLogging {
        showStandardStreams = true
    }
}

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://dl.bintray.com/kotlin/ktor" }
    maven { url  "https://dl.bintray.com/spekframework/spek-dev" }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    implementation "com.github.jknack:handlebars:$handlebars_version"
    implementation "com.github.jknack:handlebars-jackson2:$handlebars_jackson_version"
    implementation "com.openhtmltopdf:openhtmltopdf-pdfbox:$open_html_to_pdf_version"
    implementation "com.openhtmltopdf:openhtmltopdf-slf4j:$open_html_to_pdf_version"
    implementation "com.openhtmltopdf:openhtmltopdf-svg-support:$open_html_to_pdf_version"

    implementation "org.jsoup:jsoup:$jsoup_version"
    implementation "com.fasterxml.jackson.core:jackson-core:$jackson_version"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jackson_version"
    implementation "javax.xml.bind:jaxb-api:2.1"

    implementation "io.ktor:ktor-server-netty:$ktor_version"
    implementation "io.prometheus:simpleclient_common:$prometheus_version"
    implementation "io.prometheus:simpleclient_hotspot:$prometheus_version"

    implementation "ch.qos.logback:logback-classic:$logback_version"
    implementation "net.logstash.logback:logstash-logback-encoder:$logstash_encoder_version"

    testImplementation 'org.spekframework.spek2:spek-dsl-jvm:2.0.0-alpha.1'
    testImplementation 'org.amshove.kluent:kluent:1.39'
    testRuntimeOnly "org.spekframework.spek2:spek-runtime-jvm:2.0.0-alpha.1"
    testRuntimeOnly 'org.spekframework.spek2:spek-runner-junit5:2.0.0-alpha.1'

    testImplementation "org.verapdf:validation-model:1.4.5"
    testImplementation "com.squareup.okhttp3:okhttp:3.11.0"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
